(ns kotoba.genome.core
  "Data-driven kotoba Genome domain model. Pure CLJC, host runners injected outside the browser."
  (:require [kotoba.lang.text :as str]))

(def stages ["Sample intake" "Read QC" "Alignment" "Variant calling" "Annotation" "Interpretation" "Report" "Archive"])

(def artifact-formats
  [{:id :sequence/fastq :extensions [".fastq" ".fq"] :description "raw reads" :phases ["qc" "alignment"]} {:id :sequence/fasta :extensions [".fasta" ".fa"] :description "reference or contigs" :phases ["reference" "assembly"]} {:id :alignment/bam :extensions [".bam" ".sam"] :description "read alignment" :phases ["alignment" "variant-calling"]} {:id :variant/vcf :extensions [".vcf"] :description "variants" :phases ["annotation" "review"]} {:id :annotation/gff :extensions [".gff" ".gtf"] :description "feature annotation" :phases ["annotation" "reporting"]}])

(def runner-adapters
  [{:id :runner/fastqc :name "FASTQ QC" :software :sw/fastqc :operation :op/qc :formats [:sequence/fastq] :command ["fastqc" "$inputs" "-o" "out/qc"]} {:id :runner/bwa-align :name "BWA alignment" :software :sw/bwa :operation :op/align :formats [:sequence/fastq :sequence/fasta] :command ["bwa" "mem" "$reference" "$reads"]} {:id :runner/vcf-annotate :name "VCF annotation gate" :software :sw/kotoba-genome :operation :op/annotate :formats [:variant/vcf :annotation/gff] :command ["clojure" "-M" "-m" "kotoba.genome.runner" "annotate"]}])

(def coverage-metrics ["Read QC" "Reference integrity" "Alignment evidence" "Variant confidence" "Annotation trace" "Report review"])

(defn extension [filename]
  (when filename
    (let [parts (str/split filename #"\.")]
      (when (< 1 (count parts))
        (str "." (str/lower (last parts)))))))

(defn classify-artifact [filename]
  (let [ext (extension filename)]
    (or (some (fn [{:keys [id extensions description phases]}]
                (when (some #{ext} extensions)
                  {:artifact/id id
                   :artifact/path filename
                   :artifact/description description
                   :artifact/phases phases}))
              artifact-formats)
        {:artifact/id :artifact/unknown
         :artifact/path filename
         :artifact/description "Unknown imported artifact"
         :artifact/phases []})))

(defn score [project]
  (let [stage (:stage project 0)
        artifacts (:artifacts project [])
        approvals (set (:approvals project))
        artifact-ratio (min 1.0 (/ (count artifacts) (max 1 (count artifact-formats))))
        approval-ratio (min 1.0 (/ (count approvals) 4))
        stage-ratio (/ stage (dec (count stages)))]
    {:score/stage stage-ratio
     :score/artifacts artifact-ratio
     :score/approvals approval-ratio
     :score/overall (Math/round (double (* 100 (/ (+ stage-ratio artifact-ratio approval-ratio) 3))))}))

(defn runner-plan [artifacts]
  {:job/schema 1
   :job/kind :genome/runner-plan
   :job/mode :dry-run-until-host-approved
   :job/adapters
   (mapv (fn [{:keys [id name software operation formats command]}]
           (let [inputs (filter #(some #{(:artifact/id %)} formats) artifacts)]
             {:job.adapter/id id
              :job.adapter/name name
              :job.adapter/software software
              :job.adapter/operation operation
              :job.adapter/status (if (seq inputs) :ready :missing-inputs)
              :job.adapter/inputs (mapv #(select-keys % [:artifact/id :artifact/path]) inputs)
              :job.adapter/command {:command/argv command
                                    :command/input-paths (mapv :artifact/path inputs)
                                    :command/policy {:network :deny
                                                     :filesystem :workspace-only
                                                     :approval :required-before-exec}}}))
         runner-adapters)})

(defn coverage-assessment [project runner-results]
  (let [evidence (count runner-results)
        base (:score/overall (score project))
        rows (mapv (fn [metric idx]
                     {:coverage/id metric
                      :coverage/source (if (pos? evidence) :source/runner-result :source/stage-model)
                      :coverage/status (if (> (+ base (* idx 7)) 55) :passed :pending)
                      :coverage/score (min 100 (+ base (* idx 5) (* evidence 8)))})
                   coverage-metrics
                   (range))]
    {:coverage/score (if (seq rows)
                       (Math/round (double (/ (reduce + (map :coverage/score rows)) (count rows))))
                       0)
     :coverage/rows rows}))

(defn co-sientist-review [project runner-results]
  (let [s (score project)
        c (coverage-assessment project runner-results)
        blockers (cond-> []
                   (< (:score/artifacts s) 0.35) (conj :blocker/artifact-coverage-low)
                   (< (:score/approvals s) 0.5) (conj :blocker/policy-approval-low)
                   (< (:coverage/score c) 65) (conj :blocker/runner-evidence-low))]
    {:review/kind :co-sientist/quality-uiux
     :review/quality (:score/overall s)
     :review/coverage (:coverage/score c)
     :review/maturity (cond
                        (and (>= (:score/overall s) 85) (>= (:coverage/score c) 85)) :mrl/production-candidate
                        (and (>= (:score/overall s) 65) (>= (:coverage/score c) 65)) :mrl/pilot-ready
                        (>= (:score/overall s) 45) :mrl/engineering-ready
                        :else :mrl/concept)
     :review/blockers blockers}))
